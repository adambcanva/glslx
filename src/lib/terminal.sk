namespace Terminal {
  enum Color {
    DEFAULT
    BOLD
    GRAY
    RED
    GREEN
    YELLOW
    BLUE
    MAGENTA
    CYAN

    def toEscapeCode int {
      return colorToEscapeCode[self]
    }
  }

  const colorToEscapeCode IntMap<int> = {
    Color.DEFAULT: 0,
    Color.BOLD: 1,
    Color.GRAY: 90,
    Color.RED: 31,
    Color.GREEN: 32,
    Color.YELLOW: 33,
    Color.BLUE: 34,
    Color.MAGENTA: 35,
    Color.CYAN: 36,
  }

  def setColor(color Color)
  def width int
  def height int
  def print(text string)
  def flush
  def write(text string)

  if TARGET == .JAVASCRIPT {
    def setColor(color Color) {
      if dynamic.process.stdout.isTTY {
        write("\x1B[0;\(color.toEscapeCode)m")
      }
    }

    def width int {
      return dynamic.process.stdout.columns
    }

    def height int {
      return dynamic.process.stdout.rows
    }

    def print(text string) {
      write(text + "\n")
    }

    def flush {
    }

    def write(text string) {
      dynamic.process.stdout.write(text)
    }
  }
}
